allprojects {
    apply plugin: "java"
}

group = project.mavenGroup
version = project.modVersion
archivesBaseName = project.modid

subprojects {
    repositories {
        mavenCentral()
        maven {
            name = "MeshMC"
            url = "https://maven.meshmc.net"
        }
        maven {
            name = "Minecraft"
            url = "https://libraries.minecraft.net"
        }
        maven {
            name = "SpongePowered"
            url = "https://repo.spongepowered.org/repository/maven-public/"
        }
        maven {
            name = "Fabric"
            url = "https://maven.fabricmc.net/"
        }
    }

    dependencies {
        implementation "net.meshmc:mesh-loader:$loaderVersion"
        implementation "dev.tigr:simpleevents:1.3"
    }
}

configurations {
    embeddedLibs
    embeddedJars
}

dependencies {
    embeddedLibs(project(":core")) {
        transitive = false
    }

    subprojects.forEach {
        if(it.name.matches("v[0-9]+_[0-9]+_([0-9]+|x)")) embeddedJars(it) {
            transitive = false
        }
    }
}

jar {
    from {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        configurations.embeddedLibs.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    from(configurations.embeddedJars)
}